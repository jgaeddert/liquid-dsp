#
# This allows the user to install the library and headers from their host system
#

# Find the install manifest
if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR
    "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt"
  )
endif()

# Get all of the installed files
file(STRINGS "@CMAKE_BINARY_DIR@/install_manifest.txt" files)

# Remove every file in the manifest
foreach(file ${files})
  message(STATUS "Uninstalling: $ENV{DESTDIR}${file}")

  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    # Remove the file
    execute_process(
      COMMAND "@CMAKE_COMMAND@" -E remove "$ENV{DESTDIR}${file}"
      RESULT_VARIABLE rm_retval
    )
    # Print an error if we couldn't remove the file
    if(rm_retval)
      message(FATAL_ERROR "Problem removing $ENV{DESTDIR}${file}")
    endif()
  else()
    # Print a message if the file doesn't exist
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach()
