# reference: https://gitlab.kitware.com/cmake/community/-/wikis/FAQ
# section: can-i-do-make-uninstall-with-cmake
cmake_minimum_required(VERSION 3.0)

if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR
    "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt"
  )
endif()

file(STRINGS "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
foreach(file ${files})
  message(STATUS "Uninstalling: $ENV{DESTDIR}${file}")
  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    execute_process(
      COMMAND "@CMAKE_COMMAND@" -E remove "$ENV{DESTDIR}${file}"
      RESULT_VARIABLE rm_retval
      )
    if(rm_retval)
      message(FATAL_ERROR "Problem removing $ENV{DESTDIR}${file}")
    endif()
  else()
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach()
